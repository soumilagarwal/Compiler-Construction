<mainFunction> -> MAIN SQUARE_OPEN SQUARE_CLOSED <stmtAndFunctionDefs> END .
<stmtAndFunctionDefs> -> <stmtOrFunctionDef> <moreStmtOrFunctionDef> .
<moreStmtOrFunctionDef> -> <stmtOrFunctionDef> <moreStmtOrFunctionDef> | EPSILON .
<stmtOrFunctionDef> -> <stmt> | <functionDef> .
<stmt> -> <declarationStmt> | <assignmentStmt_type1> | <assignmentStmt_type2> | <ifStmt> | <ioStmt> | <funCallStmt> SEMICOLON .
<functionDef> -> FUNCTION SQUARE_OPEN <parameter_list> SQUARE_CLOSED ASSIGNOP FUNID SQUARE_OPEN <parameter_list> SQUARE_CLOSED <stmtAndFunctionDefs> END SEMICOLON .
<parameter_list> -> <type> IDENTIFIER <remainingList> .
<type> -> KEYWORD_INT | KEYWORD_FLOAT | KEYWORD_STRING | MATRIX .
<remainingList> -> COMMA <type> IDENTIFIER <remainingList> | EPSILON .
<declarationStmt> -> <type> <var_list> SEMICOLON .
<var_list> -> IDENTIFIER <more_ids> .
<more_ids> -> COMMA IDENTIFIER <more_ids> | EPSILON .
<assignmentStmt_type1> -> <leftHandSide_singleVar> ASSIGNOP <rightHandSide_type1> SEMICOLON .
<assignmentStmt_type2> -> <leftHandSide_listVar> ASSIGNOP <rightHandSide_type2> SEMICOLON .
<leftHandSide_singleVar> -> IDENTIFIER .
<leftHandSide_listVar> -> SQUARE_OPEN <var_list> SQUARE_CLOSED .
<rightHandSide_type1> -> <arithmeticExpression> | <sizeExpression> | <funCallStmt> .
<rightHandSide_type2> -> <sizeExpression> | <funCallStmt> .
<sizeExpression> -> SIZE IDENTIFIER .
<ifStmt> -> KEYWORD_IF PARENTHESES_OPEN <booleanExpression> PARENTHESES_CLOSED <stmt> <otherStmts> <elseStmt> .
<elseStmt> -> KEYWORD_ELSE <stmt> <otherStmts> ENDIF SEMICOLON | ENDIF SEMICOLON .
<otherStmts> -> <stmt> <otherStmts> | EPSILON .
<ioStmt> -> READ PARENTHESES_OPEN IDENTIFIER PARENTHESES_CLOSED SEMICOLON | PRINT PARENTHESES_OPEN IDENTIFIER PARENTHESES_CLOSED SEMICOLON .
<funCallStmt> -> FUNID PARENTHESES_OPEN <inputParameterList> PARENTHESES_CLOSED .
<inputParameterList> -> <var> <listVar> | EPSILON .
<listVar> -> COMMA <var> <listVar> | EPSILON .
<arithmeticExpression> -> <arithmeticTerm> <moreTerms_one> .
<moreTerms_one> -> <operator_lowPrecedence> <arithmeticTerm> <moreTerms_one> | EPSILON .
<arithmeticTerm> -> <arithmeticFactor> <moreTerms_two> .
<moreTerms_two> -> <operator_highPrecedence> <arithmeticFactor> <moreTerms_two> | EPSILON .
<arithmeticFactor> -> PARENTHESES_OPEN <arithmeticExpression> PARENTHESES_CLOSED | <var> .
<operator_lowPrecedence> -> ADD | SUB .
<operator_highPrecedence> -> MUL | DIV .
<booleanExpression> -> PARENTHESES_OPEN <booleanExpression> PARENTHESES_CLOSED <logicalOp> PARENTHESES_OPEN <booleanExpression> PARENTHESES_CLOSED | NOT PARENTHESES_OPEN <booleanExpression> PARENTHESES_CLOSED | <constrainedVars> <relationalOp> <constrainedVars> .
<constrainedVars> -> IDENTIFIER | INT_NUM | FLOAT_NUM .
<var> -> IDENTIFIER <matrixElement> | INT_NUM | FLOAT_NUM | STR | <matrix> .
<matrix> -> SQUARE_OPEN <rows> SQUARE_CLOSED .
<rows> -> <row> <moreRows> .
<moreRows> -> SEMICOLON <row> <moreRows> | EPSILON .
<row> -> INT_NUM <remainingColElements> .
<remainingColElements> -> COMMA INT_NUM <remainingColElements> |  EPSILON .
<matrixElement> -> SQUARE_OPEN INT_NUM COMMA INT_NUM SQUARE_CLOSED | EPSILON .
<logicalOp> -> AND | OR .
<relationalOp> -> LT | LE | EQ | GT | GE | NE .

